$bin?=@Objects/controls-leader.elf
$BPS_bin?=@Renode/bpsSim.elf
$platform?=@Renode/stm32f413.repl

macro reset
"""
    sysbus LoadELF $bin
"""

macro show_printUart
"""
    emulation CreateServerSocketTerminal 49152 "debug-uart-con" false
    connector Connect sysbus.usart2 debug-uart-con
"""

macro show_dispUart
"""
    emulation CreateServerSocketTerminal 49153 "disp-uart-con" false
    connector Connect sysbus.usart3 disp-uart-con
"""

set periphWriteHook """
print "START WRITE HOOK" 
print 'Periph: %s'%self
print 'Value: %s'%hex(value)
print 'Offset: %s'%hex(offset)
print "\n"
"""

set periphReadHook """
print "START READ HOOK"
print 'Periph: %s'%self
print 'Value: %s'%hex(value)
print 'Offset: %s'%hex(offset)
print "\np"
"""

set can1_WriteHook """
if(hex(offset) == "0x180L"):
    print('MSG ID: %s'%hex(value>>21))
elif(hex(offset) == "0x188L"):
    print('Data Lower Half: %s'%hex(value))
elif(hex(offset) == "0x18cL"):
    print('Data Upper Half: %s'%hex(value))
    print("\n")
"""

emulation CreateCANHub "carCan"
emulation CreateCANHub "motorCan"

mach create "ctrl-leader"
machine LoadPlatformDescription $platform
sysbus LoadELF $bin
logLevel 3
sysbus SetHookBeforePeripheralWrite sysbus.can1 $can1_WriteHook
runMacro $show_printUart
connector Connect sysbus.can1 carCan
connector Connect sysbus.can3 motorCan

mach create "bps-sim"
mach set "bps-sim"
machine LoadPlatformDescription $platform
sysbus LoadELF $BPS_bin
logLevel 3
sysbus SetHookBeforePeripheralWrite sysbus.can1 $can1_WriteHook
connector Connect sysbus.can1 carCan

mach set "ctrl-leader"

