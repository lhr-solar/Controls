$bin?=@Objects/controls-leader.elf
$bps_bin?=@Objects/car-sim.elf
$motor_bin?=@Objects/motor-sim.elf
$platform=@Simulator/stm32f413.repl

macro reset
"""
    sysbus LoadELF $bin
"""

macro show_uart_leader
"""
    emulation CreateServerSocketTerminal 49152 "ldr-uart-con" false
    connector Connect sysbus.usart2 ldr-uart-con
"""

macro show_uart_display
"""
    emulation CreateServerSocketTerminal 49153 "disp-uart-con" false
    connector Connect sysbus.usart3 disp-uart-con
"""

macro show_uart_motor
"""
    emulation CreateServerSocketTerminal 49154 "motor-uart-con" false
    connector Connect sysbus.usart2 motor-uart-con
"""

macro show_uart_bps
"""
    emulation CreateServerSocketTerminal 49155 "bps-uart-con" false
    connector Connect sysbus.usart2 bps-uart-con
"""

set periphWriteHook """
print "START WRITE HOOK" 
print 'Periph: %s'%self
print 'Value: %s'%hex(value)
print 'Offset: %s'%hex(offset)
print "\n"
"""

set periphReadHook """
print "START READ HOOK"
print 'Periph: %s'%self
print 'Value: %s'%hex(value)
print 'Offset: %s'%hex(offset)
print "\np"
"""

set can1_WriteHook """
if(hex(offset) == "0x180L"):
    print('MSG ID: %s'%hex(value>>21))
elif(hex(offset) == "0x188L"):
    print('Data Lower Half: %s'%hex(value))
elif(hex(offset) == "0x18cL"):
    print('Data Upper Half: %s'%hex(value))
    print("\n")
"""

emulation CreateCANHub "carCan"
emulation CreateCANHub "motorCan"

mach create "ctrl-leader"
mach set "ctrl-leader"
machine LoadPlatformDescription $platform
sysbus LoadELF $bin
logLevel 3
runMacro $show_uart_leader
runMacro $show_uart_display
machine StartGdbServer 3333
connector Connect sysbus.can1 carCan
connector Connect sysbus.can3 motorCan

mach create "bps-sim"
mach set "bps-sim"
machine LoadPlatformDescription $platform
sysbus LoadELF $bps_bin
logLevel 3
runMacro $show_uart_bps
machine StartGdbServer 3334
connector Connect sysbus.can1 carCan

mach create "motor-sim"
mach set "motor-sim"
machine LoadPlatformDescription $platform
sysbus LoadELF $motor_bin
logLevel 3
runMacro $show_uart_motor
machine StartGdbServer 3335
connector Connect sysbus.can3 motorCan

mach set "ctrl-leader"